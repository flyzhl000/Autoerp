import win.ui;
/*DSG{{*/
var winform = ..win.form(text="loading...";right=600;bottom=400;border="dialog frame";parent=...)
winform.add(
page={cls="page";text="整页控件";left=0;top=60;right=600;bottom=400;center=1;db=1;dl=1;dr=1;dt=1;edge=1;transparent=1;z=1};
static5={cls="static";text="登录名或姓名";left=9;top=36;right=88;bottom=50;dl=1;dt=1;transparent=1;z=2};
toolbar={cls="toolbar";text="工具条";left=0;top=-2;right=600;bottom=25;center=1;dl=1;dr=1;dt=1;edge=1;transparent=1;z=4};
txtText={cls="edit";left=91;top=31;right=261;bottom=53;autohscroll=false;autovscroll=false;dl=1;dt=1;edge=1;hidesel=1;tabstop=1;z=3}
)
/*}}*/

winform.listview = winform.page.listview();
winform.lvEx = winform.page.listviewEx();
winform.listview.onnotify = function(id,code,ptr){
    return winform.lvEx.openonnotify(id,code,ptr)//默认菜单,排序
}

winform.page.webService = andot.System;
winform.page.sql = "SELECT SystemUserId,LoginName,NickName,PassWord,CreateTime,IsEnable,truser,trdate FROM SystemUser";
winform.page.orderby = "LoginName";
winform.page.table = {
	{key = "rowId";name = "序号";len = 50};
	{key = "LoginName";name = "登录名";len = 100};
	{key = "NickName";name = "姓名";len = 100};
	{key = "CreateTime";name = "创建时间";len = 130};
	{key = "IsEnable";name = "状态";len = 100};
	{key = "truser";name = "异动人";len = 100};
	{key = "trdate";name = "异动时间";len = 130};
	{key = "SystemUserId";name = "主键ID";len = 0};
}
winform.page.where = function(){
	var where = "1=1 ";
	var txt = string.trim(winform.txtText.text);
	if( #txt ){
		txt = string.replace(txt, "'","''");
		where += " and ( LoginName like '%" + txt + "%' or NickName like '%" + txt + "%')"
	}
	if(tabLoginInfo["LoginName"] != "admin"){
		where += " and LoginName != 'admin'";
	}
	return where; 
}

winform.lvEx.setCellBefore = function(value, key, row, col){
	if( key == "IsEnable"){
		if( value == "1" ){
			value = "启用";
			winform.lvEx.setTextColor(row,col,gdi.RGB(0,80,0))//绿色
		}else {
			value = "禁用";
			winform.lvEx.setTextColor(row,col,gdi.RGB(255,0,0))//红色
		}
	}
	return value;
}

winform.load = function(){
	winform.page.load();//显示首页
}

winform.btnSearch = function(){
	winform.page.search();
}

winform.btnAdd = function(){
	winform.Add = true;
	winform.Edit = null;
	winform.trow = null;
	winform.loadForm("\res\system\UserAddEdit.aau")
}

winform.btnEdit = function(){
	if(not winform.page.start()){return ; }
	winform.Add = null;
	winform.Edit = true;
	winform.trow = winform.lvEx.getRowTable(winform.page.index);
	winform.loadForm("\res\system\UserAddEdit.aau")
}

winform.toolbar.btnAdd(winform.btnAdd, "新增")
winform.toolbar.btnEdit(winform.btnEdit,"修改")
winform.toolbar.btnSearch(winform.btnSearch, "查询")

winform.show()
//win.loopMessage();
return winform;
